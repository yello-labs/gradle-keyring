plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id "com.diffplug.spotless" version "5.9.0"
    id "com.gradle.plugin-publish" version "0.12.0"
}

version '0.1.0'
group 'org.yello.labs'

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy();
    implementation 'org.slf4j:slf4j-api:1.7.30'

    implementation 'org.yello-labs:keyring:0.1.0'
    runtimeOnly 'org.yello-labs:keyring:0.1.0'
    testImplementation 'junit:junit:4.13'
}

gradlePlugin {
    plugins {
        'gradle-keyring' {
            id = 'org.yello-labs.gradle-keyring'
            implementationClass = 'org.yello.labs.KeyringPlugin'
            displayName = 'Gradle-Keyring'
            description = 'Gradle plugin that applies a keyring property to projects/settings for an easy and secure way of managing secrets'
        }
    }
}

def functionalTest = sourceSets.create('functionalTest')
gradlePlugin.testSourceSets(functionalTest)

configurations[functionalTest.implementationConfigurationName].extendsFrom(configurations.testImplementation)

def functionalTestTask = tasks.register('functionalTest', Test) {
    testClassesDirs = functionalTest.output.classesDirs
    classpath = configurations[functionalTest.runtimeClasspathConfigurationName] + functionalTest.output
}

tasks.check.configure {
    dependsOn(functionalTestTask)
}

spotless {
    format 'misc', {
        // define the files to apply `misc` to
        target '*.gradle', '*.md', '.gitignore'

        // define the steps to apply to those files
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        googleJavaFormat('1.7')
    }
}

pluginBundle {
    website = 'https://github.com/yello-labs/gradle-keyring'
    vcsUrl = 'https://github.com/yello-labs/gradle-keyring.git'
    tags = ['keyring']
}
